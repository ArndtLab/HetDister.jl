module DemoInfer

using StatsBase, Distributions, HistogramBinnings
using LinearAlgebra, Statistics
using Turing, Optim
using StatsAPI
using Printf
using DynamicPPL, ForwardDiff, Accessors
using Random
using Base.Threads
using Logging
using PreallocationTools

include("Spectra/Spectra.jl")
using .Spectra

include("utils.jl")
include("mle_optimization.jl")
include("sequential_fit.jl")
include("corrections.jl")

export pre_fit, pre_fit!, demoinfer, compare_models,
    get_para, evd, sds, pop_sizes, durations,
    compute_residuals,
    adapt_histogram,
    FitResult, FitOptions


function integral_ws(edges::AbstractVector{<:Real}, mu::Float64, TN::Vector)
    a = 0.5
    last_hid_I = laplacekingmanint(edges[1] - a, mu, TN)
    weights = Vector{Float64}(undef, length(edges)-1)
    for i in eachindex(weights)
        @inbounds this_hid_I = laplacekingmanint(edges[i+1] - a, mu, TN)
        weights[i] = this_hid_I - last_hid_I
        last_hid_I = this_hid_I
    end
    weights
end

"""
    compute_residuals(h::Histogram, mu::Float64, TN::Vector; naive=false)

Compute the residuals between the observed and expected weights.
"""
function compute_residuals(h::Histogram, mu::Float64, TN::Vector; naive=false, order=10, ndt=800)
    if naive
        w_th = integral_ws(h.edges[1], mu, TN)
    else
        rs = midpoints(h.edges[1])
        bag = IntegralArrays(order, ndt, length(rs), Val{length(TN)})
        mldsmcp!(bag, 1:bag.order, rs, h.edges[1].edges, mu, 0.0, TN)
        w_th = get_tmp(bag.ys, eltype(TN)) .* diff(h.edges[1])
    end
    residuals = (h.weights - w_th) ./ sqrt.(w_th)
    @assert all(isfinite.(residuals))
    return residuals
end

"""
    compute_residuals(h1::Histogram, h2::Histogram)

When two histograms are given the std error of the difference is
used for normalization.
"""
function compute_residuals(h1::Histogram, h2::Histogram; fc1 = 1.0, fc2 = 1.0)
    @assert length(h1.weights) == length(h2.weights)
    # when both observations are zero the residual is zero
    w_ = h1.weights / fc1 .+ h2.weights / fc2
    residuals = (h1.weights / fc1 - h2.weights / fc2) ./ sqrt.(w_)
    residuals[w_ .== 0] .= 0
    @assert all(isfinite.(residuals))
    return residuals
end

function CustomEdgeVector(; lo = 1, hi = 10, nbins::Integer)
    @assert (lo > 0) && (hi > 0) && (nbins > 0) && (hi > lo)
    lo = floor(Int, lo)
    hi = ceil(Int, hi)
    edges = collect(logrange(lo, hi+1, length = nbins+1))
    for i in 2:nbins+1
        nw = ceil(Int, edges[i])
        delta = nw - edges[i]
        edges[i:end] .+= delta
    end
    edges = unique(round.(Int, edges))
    edges[1] = lo
    edges[end] = hi + 1
    @assert length(edges) == nbins + 1
    LogEdgeVector(edges)
end

"""
    adapt_histogram(segments::AbstractVector{<:Integer}; lo::Int=1, hi::Int=50_000_000, nbins::Int=200)

Build an histogram from `segments` logbinned between `lo` and `hi`
with `nbins` bins (see `HistogramBinnings.jl`).

The upper limit is adapted to the maximum observed length, so default value
is on purpose high.
"""
function adapt_histogram(segments::AbstractVector{<:Integer}; lo::Int=1, hi::Int=50_000_000, nbins::Int=200)
    h_obs = Histogram(CustomEdgeVector(;lo, hi, nbins))
    append!(h_obs, segments)
    l = findlast(h_obs.weights .> 0)
    while h_obs.edges[1].edges[l+1] + 1 < hi
        hi = h_obs.edges[1].edges[l+1]
        h_obs = Histogram(CustomEdgeVector(;lo, hi, nbins))
        append!(h_obs, segments)
        l = findlast(h_obs.weights .> 0)
    end
    # edges = h_obs.edges[1].edges
    # T = eltype(edges)
    # nedges = T[]
    # weights = h_obs.weights
    # counter = 0
    # for i in eachindex(weights)
    #     if i == 1
    #         push!(nedges, edges[i])
    #     elseif weights[i] == 0
    #         # record row of zeros
    #         counter += 1
    #     elseif counter > 0
    #         # enter here only when is not zero following a zero
    #         hi = edges[i+1]
    #         lo = edges[i-counter-1]
    #         mid = floor(sqrt(lo * hi))
    #         push!(nedges, mid)
    #         counter = 0
    #     elseif counter == 0
    #         # enter here when non zero following non zero
    #         push!(nedges, edges[i])
    #     end
    # end
    # push!(nedges, edges[end])
    # h_obs = Histogram(LogEdgeVector(nedges))
    # append!(h_obs, segments)
    return h_obs
end

"""
    compare_mlds(segs1::Vector{Int}, segs2::Vector{Int}; lo = 1, hi = 1_000_000, nbins = 200)

Build two histograms from `segs1` and `segs2`, rescale them for number and
average heterozygosity and return four vectors containing respectively
common midpoints for bins, the two rescaled weights and variances of
the difference between weights.
"""
function compare_mlds(segs1::AbstractVector{<:Integer}, segs2::AbstractVector{<:Integer}; lo = 1, hi = 1_000_000, nbins = 200)
    theta1 = 1/mean(segs1)
    theta2 = 1/mean(segs2)
    h1 = Histogram(LogEdgeVector(lo = lo, hi = hi, nbins = nbins))
    append!(h1, segs1)
    h2 = Histogram(LogEdgeVector(lo = lo, hi = hi, nbins = nbins))
    append!(h2, segs2)
    return compare_mlds!(h1, h2, theta1, theta2)
end
function compare_mlds!(h1, h2, theta1, theta2) # add !
    # 1 is the target lattice, i.e. with biggest theta
    length(h1.weights) == length(h2.weights) && @assert any(h1.weights .!= h2.weights)
    @assert theta1 != theta2
    swap = false
    if theta1 < theta2
        temp = deepcopy(h1)
        h1 = deepcopy(h2)
        h2 = temp
        theta1, theta2 = theta2, theta1
        swap = true
    end
    edges1 = h1.edges[1].edges * theta1
    edges2 = h2.edges[1].edges * theta2
    tw = zeros(Float64, length(h1.weights))
    w2 = h2.weights
    factor = sum(h1.weights) / sum(h2.weights)
    t = 1
    f = 1
    while t < length(edges1) && f < length(edges2) && h1.weights[t] > 0 && w2[f] > 0
        st, en = edges1[t], edges1[t+1]
        width = edges2[f+1] - edges2[f]
        if st <= edges2[f] < edges2[f+1] < en
            tw[t] += w2[f]
            f += 1
        elseif st <= edges2[f] < en < edges2[f+1]
            tw[t] += w2[f] * (en - edges2[f]) / width
            t += 1
        elseif edges2[f] < st <= edges2[f+1] < en
            if f == 1
                tw[t] += w2[f]
            else
                tw[t] += w2[f] * (edges2[f+1] - st) / width
            end
            f += 1
        elseif edges2[f] <= st < en < edges2[f+1]
            tw[t] += w2[f] * (en - st) / width
            t += 1
        else
            if edges2[f+1] < st && t == 1
                tw[t] += w2[f]
                f += 1
            else
                f > t ? t+=1 : f+=1
                @error "disjoint bins"
            end
        end
    end
    
    rs = midpoints(h1.edges[1]) * theta1
    sigmasq = h1.weights .+ tw * factor^2
    maxl = min(t,f,length(h1.weights),length(tw))
    if swap
        return rs[1:maxl], tw[1:maxl] * factor, h1.weights[1:maxl], sigmasq[1:maxl]
    else
        return rs[1:maxl], h1.weights[1:maxl], tw[1:maxl] * factor, sigmasq[1:maxl]
    end
end

end
